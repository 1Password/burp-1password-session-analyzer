plugins {
    id 'java'
    id 'net.nemerosa.versioning' version '3.1.0'
}

group 'com.1password.burp-analyzer'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'net.portswigger.burp.extender', name: 'burp-extender-api', version: '2.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4.2'
    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '3.3.0'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

versioning {
    ext.makeVersion = {
        if(project.hasProperty('release')) {
            def dateFormat = new java.text.SimpleDateFormat("yyyy-MM-dd")
            dateFormat.setTimeZone(TimeZone.getTimeZone("UTC"))
            def today = dateFormat.format(new Date())
            return String.format("%s-%s", versioning.info.full, today)
        } else {
            return "dev"
        }
    }
}

version versioning.ext.makeVersion()

task fatJar(type: Jar) {
    duplicatesStrategy = 'exclude'
    archiveBaseName = "${rootProject.name}"
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test {
    useJUnit()
}